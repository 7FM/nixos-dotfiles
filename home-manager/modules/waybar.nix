{ config, pkgs, lib, ... }:

let
  cfg = config.custom.hm.modules;

  laptopDisplay = cfg.sway.laptopDisplay;

  hwmonPath = cfg.waybar.hwmonPath; # sys-fs path, i.e. "/sys/class/hwmon/hwmon0/temp1_input"
  thermalZone = cfg.waybar.thermalZone; # Integer value

  gpuCfg = cfg.waybar.gpu;
  # First we create a list of the setting values
  # After that we remove the null elements and get the count of the remaining values
  gpuCfgValueCount = builtins.length (builtins.filter (x: x != null) (builtins.attrValues gpuCfg));
  # We enable gpu stats iff at least gpu stat command was given
  enableGpuStats = gpuCfgValueCount > 0;

  hmManageSway = config.custom.gui == "hm-wayland";
  enable = hmManageSway || (config.custom.gui == "wayland");

  # Waybar settings
  enableSystemdWaybar = config.wayland.windowManager.sway.enable && config.wayland.windowManager.sway.systemdIntegration;
  waybarLaptopFeatures = laptopDisplay != null;
in {
  options.custom.hm.modules = with lib; {
    waybar = {
      hwmonPath = mkOption {
        type = types.nullOr types.str;
        default = null;
        description = ''
          Specifies the sys-fs path to an hwmon.
          This might be required if the default method can not determine the cpu temperature.
        '';
      };
      thermalZone = mkOption {
        type = types.nullOr types.ints.u8;
        default = null;
        description = ''
          Thermal zone to use for the waybar cpu temperature measurements.
        '';
      };
      gpu = {
        tempCmd = mkOption {
          type = types.nullOr types.str;
          default = null;
          description = ''
            The command to use for the gpu temperature measurements.
            I.e. "cat /sys/class/drm/card0/device/hwmon/hwmon0/temp1_input"
          '';
        };
        mhzFreqCmd = mkOption {
          type = types.nullOr types.str;
          default = null;
          description = ''
            The command to use to determine the gpu clock frequency in MHz.
            I.e. "cat /sys/class/drm/card0/device/pp_dpm_sclk | egrep -o '[0-9]{0,4}Mhz \\W' | sed 's/Mhz \\*//'"
            or   "cat /sys/class/drm/card0/gt_cur_freq_mhz"
          '';
        };
        usageCmd = mkOption {
          type = types.nullOr types.str;
          default = null;
          description = ''
            The command to use to determine the gpu usage in percent.
            I.e. "cat /sys/class/drm/card0/device/gpu_busy_percent"
          '';
        };
      };
    };
  };

  config = lib.mkIf enable {

    home.packages = with pkgs; [
      # needed for waybar customization
      font-awesome

      gnome.gnome-calendar
      pavucontrol # GUI to control pulseaudio settings
      wlogout # logout menu
      networkmanagerapplet # NetworkManager Front-End
      wpa_supplicant_gui # wpasupplicant Front-End

      bc # needed for gpu clock speed calculation
    ] ++ lib.optionals waybarLaptopFeatures [ 
      brightnessctl
    ];

    # Waybar configuration
    programs.waybar = {
      enable = true;
      systemd = {
        enable = enableSystemdWaybar;
        target = "sway-session.target";
      };
      package = (pkgs.waybar.override { withMediaPlayer = true; });

      settings = [{
        modules-left = [
          "sway/workspaces"
          "custom/scratchpad-indicator"
          "sway/mode"
        ] ++
        lib.optionals config.custom.bluetooth [
          "bluetooth"
        ] ++ [
          "network"
        ];
        modules-center = [
          "tray"
          "custom/spotify"
          "custom/media_firefox"
        ];
        modules-right = [
          "custom/mail"
          "temperature"
          "cpu"
          "memory"
        ] ++ lib.optionals enableGpuStats [
          "custom/gpu"
        ] ++ [
          #"custom/disk_home"
          "custom/disk_root"
          "pulseaudio#out"
          "pulseaudio#in"
        ] ++ lib.optionals waybarLaptopFeatures [ 
          "backlight"
          "battery"
        ] ++ [
          "idle_inhibitor"
          "clock"
          "custom/logout"
        ];

        modules = {
          # Modules configuration
          "sway/workspaces" = {
            disable-scroll = false;
            all-outputs = false;
            format = "{name}{icon}";
            # format = "{index}{icon}";
            format-icons = {
              "1:term" = " ï„ ";
              "2:web" = " ï‰¨";
              "3:code" = " ï„¡";
              "4:music" = " ï†¼";
              "5:chat" = " ïµ";
              "urgent" = " ïª";
              # "focused" = " ï†’";
              "focused" = "";
              # "default" = " ï„‘";
              "default" = "";
            };
          };
          "sway/mode" = {
            format = "{}";
          };
          "custom/disk_home" = {
            format = "ğŸ {}";
            interval = 180;
            exec = "df -h --output=avail $HOME | tail -1 | tr -d ' '";
            tooltip = false;
            escape = true;
          };
          "custom/disk_root" = {
            format = "ğŸ’½{}";
            interval = 180;
            exec = "df -h --output=avail / | tail -1 | tr -d ' '";
            tooltip = false;
            escape = true;
          };
          "custom/logout" = {
            format = "ï€£";
            on-click = "wlogout";
            on-click-right = "wlogout";
            tooltip = false;
          };
          "custom/gpu" = {
            "exec" = "\${XDG_CONFIG_HOME:-\$HOME/.config}/waybar/scripts/custom_gpu.sh";
            "return-type" = "json";
            "format" = "{}";
            "interval" = 5;
            "tooltip" = "{tooltip}";
            escape = true;
          };
          "custom/scratchpad-indicator" = {
              "interval" = 3;
              "return-type" = "json";
              "exec" = "swaymsg -t get_tree | jq --unbuffered --compact-output '( select(.name == \"root\") | .nodes[] | select(.name == \"__i3\") | .nodes[] | select(.name == \"__i3_scratch\") | .focus) as $scratch_ids | [..  | (.nodes? + .floating_nodes?) // empty | .[] | select(.id |IN($scratch_ids[]))] as $scratch_nodes | { text: \"\\($scratch_nodes | length)\", tooltip: $scratch_nodes | map(\"\\(.app_id // .window_properties.class) (\\(.id)): \\(.name)\") | join(\"\\n\") }'";
              # "format" = "ô œ {}";
              "format" = "ïŒ¨{}";
              "on-click" = "exec swaymsg 'scratchpad show'";
              "on-click-right" = "exec swaymsg 'move scratchpad'";
              escape = true;
          };
          "temperature" = {
            critical-threshold = 80;
            # format-critical = "{temperatureC:>3}Â°C {icon}";
            format = "<span color='#e88939'>{icon}</span> {temperatureC}Â°C";
            format-icons = [
              "ï‹‹" # Icon: temperature-empty
              "ï‹Š" # Icon: temperature-quarter
              "ï‹‰" # Icon: temperature-half
              "ï‹ˆ" # Icon: temperature-three-quarters
              "ï‹ˆ" # Icon: temperature-full
            ];
            tooltip = false;
          } // (if (hwmonPath != null) then { hwmon-path = hwmonPath; } else {})
            // (if (thermalZone != null) then { thermal-zone = thermalZone; } else {});
          "cpu" = {
            format = "ï‹›{usage:>3}%";
            tooltip = false;
            on-click = "alacritty --command htop";
            on-click-right = "alacritty --command htop";
          };
          "memory" = {
            format = "ïƒ‰ {used:0.1f}G";
            on-click = "alacritty --command htop";
            on-click-right = "alacritty --command htop";
          };
          "custom/mail" = {
            format = "ğŸ“© {}";
            interval = 180;
            exec = "notmuch count 'tag:flagged OR (tag:inbox AND NOT tag:killed AND NOT tag:spam AND tag:unread)'";
            escape = true;
          };
          "bluetooth" = {
              interval = 30;
              format-on = "<span color='#589df6'>ïŠ“</span>";
              format-off = "ïŠ”";
              format-disabled = "ïŠ”";
              format-connected = "<span color='#589df6'>ïŠ“</span> {device_alias}";
              # format-connected-battery = "<span color='#589df6'>ïŠ“</span> {device_alias} {device_battery_percentage}%";
              format-connected-battery = "<span color='#589df6'>ïŠ“</span> {device_alias}{icon}";
              tooltip-format = "{controller_alias}\t{controller_address}\n\n{num_connections} connected";
              tooltip-format-connected = "{controller_alias}\t{controller_address}\n\n{num_connections} connected\n\n{device_enumerate}";
              tooltip-format-enumerate-connected = "{device_alias}\t{device_address}";
              tooltip-format-enumerate-connected-battery = "{device_alias}\t{device_address}\t{device_battery_percentage}%";
              # Bluetooth battery status icons from low to high
              format-icons = ["ï‰„" "ï‰ƒ" "ï‰‚" "ï‰" "ï‰€"];
              #format-icons = ["ï–" "ï•»" "ï•½" "ï–€" "ï•¸"];
              on-click = "blueman-manager";
          };
          "network" = {
            family = "ipv4";
            format-wifi = "<span color='#589df6'>ï‡«</span> <span color='gray'>{essid}</span> <span color='#589df6'>{signalStrength}%</span> <span color='#589df6'>â‡µ</span> {bandwidthDownBits}|{bandwidthUpBits}";
            format-ethernet = "ï›¿ {ifname}: {ipaddr} <span color='#589df6'>â‡µ</span> {bandwidthDownBits}|{bandwidthUpBits}";
            format-linked = "ï›¿ {ifname} (No IP) <span color='#589df6'>â‡µ</span> {bandwidthDownBits}|{bandwidthUpBits}";
            format-disconnected = "âš  Disconnected";
            interval = 2;
            on-click = "nm-connection-editor";
            on-click-right = "nm-connection-editor";
            tooltip = false;
          };
          "backlight" = {
            device = "intel_backlight";
            format = "{icon} {percent}%";
            format-icons = ["ğŸ”…" "ğŸ”†"];
          };
          "pulseaudio#out" = {
            # scroll-step = 1; # %, can be a float
            format = "{icon}{volume:>3}%";
            format-muted = "ğŸ”‡  0%";
            format-bluetooth = "{icon}ïŠ”{volume:>3}%";
            format-bluetooth-muted = "ğŸ”‡ïŠ”  0%";

            format-source = "";
            format-source-muted = "";

            format-icons = {
              "headphone" = "ï€¥";
              "hands-free" = "ï–";
              "headset" = "ï–";
              "phone" = "ï‚•";
              "portable" = "ï‚•";
              "car" = "ï†¹";
              "default" = ["ğŸ”ˆ" "ğŸ”‰" "ğŸ”Š"];
            };
            on-click = "pactl set-sink-mute @DEFAULT_SINK@ toggle";
            on-click-right = "pavucontrol";
            tooltip = false;
          };
          "pulseaudio#in" = {
            format = "{format_source}";
            format-muted = "{format_source}";
            format-bluetooth = "{format_source}";
            format-bluetooth-muted = "{format_source}";

            format-source = "ï„°{volume:>3}%";
            format-source-muted = "ï„±  0%";

            on-click = "pactl set-source-mute @DEFAULT_SOURCE@ toggle";
            on-click-right = "pavucontrol";

            on-scroll-up = "pactl set-source-volume @DEFAULT_SOURCE@ +1%";
            on-scroll-down = "pactl set-source-volume @DEFAULT_SOURCE@ -1%";
            tooltip = false;
          };
          "clock" = {
            interval = 60;
            timezone = "Europe/Berlin";
            format = "{:%H:%M|%e %b}";
            tooltip-format = "{:%d-%m-%Y | %H:%M}";
            on-click = "gnome-calendar";
          };
          "battery" = {
            states = {
              "good" = 80;
              "warning" = 20;
              "critical" = 10;
            };
            format = "{icon}{capacity:>3}%{time}";
            format-time = " {H}h{M}m";
            format-charging = "{icon}<span color='#e88939'>ïƒ§</span>{capacity:>3}%{time}";
            # format-charging = "{icon}<span color='#e88939'>ï—§</span>{capacity:>3}%{time}";
            format-plugged =  "{icon}<span color='#e88939'>ï‡¦</span>{capacity:>3}%{time}";
            # format-good = "", # An empty format will hide the module
            # format-full = "";
            format-icons = ["ï‰„" "ï‰ƒ" "ï‰‚" "ï‰" "ï‰€"];
          };
          "idle_inhibitor" = {
            format = "<span color='#589df6'>{icon}</span>";
            format-icons = {
              "activated" = "ï®";
              "deactivated" = "ï°";
            };
          };
          "tray" = {
            # icon-size = 21;
            spacing = 5;
          };
          "custom/spotify" = {
            format = "{icon}:{}";
            return-type = "json";
            max-length = 40;
            format-icons = {
              "spotify" = "ï†¼";
              "firefox" = "ï…§";
              "default" = "ğŸœ";
            };
            escape = true;
            # Filter player based on name
            exec = "waybar-mediaplayer.py --player spotify 2> /dev/null"; # Script in resources folder
            exec-if = "pgrep spotify";
            on-click = "playerctl -p spotify play-pause";
            on-click-right = "playerctl -p spotify next";
          };
          "custom/media_firefox" = {
            format = "{icon}:{}";
            return-type = "json";
            max-length = 40;
            format-icons = {
              "spotify" = "ï†¼";
              "firefox" = "ï…§";
              "default" = "ğŸœ";
            };
            escape = true;
            # Filter player based on name
            exec = "waybar-mediaplayer.py --player firefox 2> /dev/null"; # Script in resources folder
            exec-if = "pgrep 'Web Content'";
            on-click = "playerctl -p firefox play-pause";
            on-click-right = "playerctl -p firefox next";
          };
          "mpd" = {
            format = "{stateIcon} {consumeIcon}{randomIcon}{repeatIcon}{singleIcon}{artist} - {album} - {title} ({elapsedTime:%M:%S}/{totalTime:%M:%S}) ï€";
            format-disconnected = "Disconnected ï€";
            format-stopped = "{consumeIcon}{randomIcon}{repeatIcon}{singleIcon}Stopped ï€";
            unknown-tag = "N/A";
            interval = 2;
            consume-icons = {
              "on" = "ïƒ„ ";
            };
            random-icons = {
              "off" = "<span color=\"#f53c3c\">ï´</span> ";
              "on" = "ï´ ";
            };
            repeat-icons = {
              "on" = "ï€ ";
            };
            single-icons = {
              "on" = "ï€1 ";
            };
            state-icons = {
              "paused" = "ïŒ";
              "playing" = "ï‹";
            };
            tooltip-format = "MPD (connected)";
            tooltip-format-disconnected = "MPD (disconnected)";
          };
        };
      }];
    };

    # This enables discovering fonts that where installed with home.packages
    fonts.fontconfig.enable = true;

    xdg.configFile."waybar/style.css".source = ../configs/waybar/style.css;
    xdg.configFile."waybar/scripts/custom_gpu.sh" = {
      text = ''
        #!/bin/sh
      '' + (lib.optionalString (gpuCfg.mhzFreqCmd != null) ''
        raw_clock=$(${gpuCfg.mhzFreqCmd})
        clock=$(echo "scale=1;$raw_clock/1000" | bc | sed -e 's/^-\./-0./' -e 's/^\./0./')
      '') + (lib.optionalString (gpuCfg.tempCmd != null) ''
        raw_temp=$(${gpuCfg.tempCmd})
        temperature=$(($raw_temp/1000))
      '') + (lib.optionalString (gpuCfg.usageCmd != null) ''
        busypercent=$(${gpuCfg.usageCmd})
      '') + ''
        echo '{"text": "'' +
        (lib.optionalString (gpuCfg.mhzFreqCmd != null) '''$clock'GHz'' + lib.optionalString (gpuCfg.tempCmd != null || gpuCfg.usageCmd != null) " ") +
        (lib.optionalString (gpuCfg.tempCmd != null) ''ï‹‰ '$temperature'Â°C'' + lib.optionalString (gpuCfg.usageCmd != null) " ") +
        (lib.optionalString (gpuCfg.usageCmd != null) '''$busypercent'%'') +
        ''", "class": "custom-gpu", "tooltip": ""}'
      '';
      executable = true;
    };
    xdg.configFile."wofi".source = ../configs/wofi;
    xdg.configFile."wlogout".source = ../configs/wlogout;
  };
}
